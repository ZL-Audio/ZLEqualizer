# Write some temp files to make GitHub Actions / packaging easy

if (DEFINED ENV{CI})
    set (env_file "${PROJECT_SOURCE_DIR}/.env")
    message ("Writing ENV file for CI: ${env_file}")

    # the first call truncates, the rest append
    file(WRITE  "${env_file}" "PROJECT_NAME=${PROJECT_NAME}\n")
    file(APPEND "${env_file}" "PRODUCT_NAME=${PRODUCT_NAME}\n")
    file(APPEND "${env_file}" "VERSION=${CURRENT_VERSION}\n")
    file(APPEND "${env_file}" "BUNDLE_ID=${BUNDLE_ID}\n")
    file(APPEND "${env_file}" "COMPANY_NAME=${COMPANY_NAME}\n")

    file(RELATIVE_PATH RELATIVE_BINARY_PATH "${CMAKE_SOURCE_DIR}" "${CMAKE_BINARY_DIR}")
    set (ARTIFACTS_PATH "${RELATIVE_BINARY_PATH}/${CMAKE_PROJECT_NAME}_artefacts/${CMAKE_BUILD_TYPE}")

    file(APPEND "${env_file}" "ARTIFACTS_PATH=${ARTIFACTS_PATH}\n")
    if ("VST3" IN_LIST FORMATS)
        file(APPEND "${env_file}" "VST3_PATH=${ARTIFACTS_PATH}/VST3/${PRODUCT_NAME}.vst3\n")
    endif ()
    if ("AU" IN_LIST FORMATS)
        file(APPEND "${env_file}" "AU_PATH=${ARTIFACTS_PATH}/AU/${PRODUCT_NAME}.component\n")
    endif ()
    if ("LV2" IN_LIST FORMATS)
        file(APPEND "${env_file}" "LV2_PATH=${ARTIFACTS_PATH}/LV2/${PRODUCT_NAME}.lv2\n")
    endif ()
    if ("Standalone" IN_LIST FORMATS)
        if (CMAKE_SYSTEM_NAME STREQUAL "Darwin")
            file(APPEND "${env_file}" "Standalone_PATH=${ARTIFACTS_PATH}/Standalone/${PRODUCT_NAME}.app\n")
        elseif (CMAKE_SYSTEM_NAME STREQUAL "Windows")
            file(APPEND "${env_file}" "Standalone_PATH=${ARTIFACTS_PATH}/Standalone/${PRODUCT_NAME}.exe\n")
        else ()
            file(APPEND "${env_file}" "Standalone_PATH=${ARTIFACTS_PATH}/Standalone/${PRODUCT_NAME}\n")
        endif ()
    endif ()
endif ()
